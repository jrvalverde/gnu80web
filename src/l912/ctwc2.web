@* ctwc2.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[OUTPUT:] W                                                         
\item[INPUT:] CA                                                        
\item[INPUT:] CB                                                        
\item[INPUT:] WT                                                        
\item[INPUT:] NPIJ                                                      
\item[OUTPUT:] S                                                         
\item[INPUT:] INITW                                                     
\item[INPUT:] NBASIS                                                    
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]     NOTHING                                           
\item[Uses]  NVA     NVAVB   NVB     ZERO                           
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       TRACK                                                  
\item[Called by]   TSTARB                                                 
                                                                    
\end{description}
@a
      subroutine ctwc2(W,CA,CB,WT,NPIJ,S,INITW,NBASIS)
      implicit none
      double precision a0 , CA , CB , F42 , Four , Half , One , Onept5 , 
     &                 S , Ten , Three , Two , W , WT , Zero
      integer ia , ia1 , ib , ib1 , Ieval , ij , ija , Ioab , Ispect , 
     &        Loab , Lspect , m , Maxbuc , mu , mu1 , NBASIS , nbsq , 
     &        Noa , Noa2 , Noa3
      integer Noaob , Noava , Noavb , Nob , Nob2 , Nob3 , Nobva , 
     &        Nobvb , Novaa , Novab , Novbb , NPIJ , Nrorb , nu , Nva , 
     &        Nva2 , Nva3 , Nvavb , Nvb , Nvb2
      integer Nvb3
      dimension W(*) , CA(*) , CB(*) , WT(*) , S(*)
      logical INITW
      common /orb   / Ispect , Lspect , Nrorb , Noa , Nva , Nob , Nvb , 
     &                Noaob , Noava , Noavb , Nobva , Nobvb , Nvavb , 
     &                Noa2 , Noa3 , Nob2 , Nob3 , Nva2 , Nva3 , Nvb2 , 
     &                Nvb3 , Novaa , Novab , Novbb , Maxbuc , Ieval , 
     &                Ioab , Loab
      common /const / Zero , Half , One , Onept5 , Two , Three , Four , 
     &                Ten , F42
C
C     ******************************************************************
C1ctwc2
C     subroutine ctwc2(w,ca,cb,wt,npij,s,initw)
C     evaluates  w(i,j,a,b) = sum  ca(mu,a) * cb(nu,b) * wt(i,j,mu,nu)
C                            mu,nu
C     for all i,j,a,b   (spincase ab)
C
C     dec 76/seeger and krishnan
C
C     ******************************************************************
C
C
C
      call track('CTWC2 ')
C
      if ( NPIJ.le.0 ) return
      nbsq = NBASIS**2
      if ( Nvavb.le.0 ) return
C
      m = 0
      ija = 0
      do 100 ij = 1 , NPIJ
         ia1 = 0
         do 50 ia = 1 , Nva
C
            do 20 nu = 1 , NBASIS
               a0 = Zero
               mu1 = ija + nu
               do 10 mu = 1 , NBASIS
                  a0 = a0 + CA(ia1+mu)*WT(mu1)
                  mu1 = mu1 + NBASIS
 10            continue
               S(nu) = a0
 20         continue
            ib1 = 0
            if ( INITW ) then
C
C     w is incremented
               do 30 ib = 1 , Nvb
                  a0 = Zero
                  do 25 nu = 1 , NBASIS
                     a0 = a0 + CB(ib1+nu)*S(nu)
 25               continue
                  m = m + 1
                  W(m) = W(m) + a0
                  ib1 = ib1 + NBASIS
 30            continue
            else
C
C     w is initialized
               do 40 ib = 1 , Nvb
                  a0 = Zero
                  do 35 nu = 1 , NBASIS
                     a0 = a0 + CB(ib1+nu)*S(nu)
 35               continue
                  m = m + 1
                  W(m) = a0
                  ib1 = ib1 + NBASIS
 40            continue
            endif
C
            ia1 = ia1 + NBASIS
 50      continue
         ija = ija + nbsq
 100  continue
C
      return
C
      end
@* INDEX.
