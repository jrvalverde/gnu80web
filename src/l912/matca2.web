@* matca2.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[INPUT:] NIJ                                                       
\item[INPUT:] NKL                                                       
\item[INPUT:] NMN                                                       
\item[PASSED:] IBUC1                                                     
\item[PASSED:] IBUC2                                                     
\item[PASSED:] IBUC3                                                     
\item[INPUT:] ISIGN                                                     
\item[INPUT:] IOPT                                                      
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]    V                                                      
\item[Uses]  MDV     V       ZERO                                   
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       FILEIO  LNK1E   TRACK                                  
\item[Called by]   DD2     DD3                                            
                                                                    
\end{description}
@a
      subroutine matca2(NIJ,NKL,NMN,IBUC1,IBUC2,IBUC3,ISIGN,IOPT)
      implicit none
      double precision F42 , Four , Half , One , Onept5 , Ten , Three , 
     &                 thresh , Two , V , Zero
      integer i , IBUC1 , IBUC2 , IBUC3 , ibucfl , ij , ijbloc , ijind , 
     &        ijleft , ind1 , ind1s , ind2 , ind3 , indfl , IOPT , 
     &        iopt1 , ISIGN , iv1 , iv2 , iv21
      integer iv3 , iv31 , kl , klbloc , klleft , leng , max1 , max2 , 
     &        max3 , Mdv , mdv3 , mdvlft , mn , nfull , nfull1 , 
     &        nfull2 , nfull3 , NIJ , nijkl , nijmn
      integer NKL , nklmn , NMN , nsum
      common /v     / V(20000) , Mdv
      common /const / Zero , Half , One , Onept5 , Two , Three , Four , 
     &                Ten , F42
      data thresh/1.D-8/
C
C***********************************************************************
C1matca2
C
C     accepts nkl*nij matrix a from ibuc1
C     accepts nij*nmn matrix b from ibuc2
C     forms   nkl*nmn matrix c in ibuc3
C          c(kl,mn) = sum (ij) a(kl,ij)*b(ij,mn)
C          note that this means that ij is the slower running subscript
C          in b and the faster running subscript in a.
C
C     isign ... .gt.0: sums positive
C               .lt.0: sums negative
C
C     iopt ... 0: c is initialized
C              1: c is added to the previously calculated c
C
C     this routine checks for zeroes in ibuc1.
C
C
C     sept,77  krishnan
C
C***********************************************************************
C
C
C
      call track('MATCA2')
C
      if ( NIJ*NKL*NMN.le.0 ) return
C
      iopt1 = IOPT + 1
      mdv3 = Mdv/3
      ibucfl = 0
      nijkl = NIJ*NKL
      nijmn = NIJ*NMN
      nklmn = NKL*NMN
      nsum = nijkl + nijmn + nklmn
      mdvlft = Mdv
      max2 = float(nijmn)*float(mdvlft)/float(nsum)
      if ( max2.lt.NMN ) max2 = NMN
      if ( nijmn.le.mdv3 ) max2 = nijmn
      mdvlft = mdvlft - max2
      nsum = nsum - nijmn
      max1 = float(nijkl)*float(mdvlft)/float(nsum)
      if ( max1.lt.NIJ ) max1 = NIJ
      max3 = Mdv - max1 - max2
      if ( max3.lt.NMN ) then
         max3 = NMN
         max2 = Mdv - max1 - max3
         if ( max2.lt.NMN ) then
            max2 = NMN
            max1 = Mdv - max2 - max3
            if ( max1.lt.NIJ ) call lnk1e
         endif
      endif
      if ( max2.ge.nijmn ) ibucfl = 1
C
      iv1 = 0
      iv2 = max1
      iv3 = max1 + max2
      iv21 = iv2 + 1
      iv31 = iv3 + 1
      nfull1 = max1/NIJ
      nfull2 = max2/NMN
      nfull3 = max3/NMN
      nfull = min0(nfull1,nfull3)
C
C     start read,write operations.
      call fileio(2,-IBUC1,0,0,0)
      call fileio(2,-IBUC3,0,0,0)
      call fileio(1,-IBUC3,0,0,0)
      klleft = NKL
C
      indfl = 0
 100  klbloc = min0(klleft,nfull)
      klleft = klleft - klbloc
      leng = klbloc*NIJ
      call fileio(2,IBUC1,leng,V,0)
      leng = klbloc*NMN
      if ( iopt1.eq.2 ) then
C
         call fileio(2,IBUC3,leng,V(iv31),0)
      else
C
         do 150 i = 1 , leng
            V(iv3+i) = Zero
 150     continue
      endif
C
      ijleft = NIJ
      ijind = 0
      call fileio(2,-IBUC2,0,0,0)
 200  ijbloc = min0(ijleft,nfull2)
      ijleft = ijleft - ijbloc
      leng = ijbloc*NMN
      if ( ibucfl.eq.0 .or. indfl.eq.0 )
     &     call fileio(2,IBUC2,leng,V(iv21),0)
      indfl = 1
C
C     form product in v3.
      ind1s = ijind
      ind3 = iv3
      if ( ISIGN.lt.0 ) then
C
         do 250 kl = 1 , klbloc
            ind1 = ind1s
            ind2 = iv2
            do 220 ij = 1 , ijbloc
               ind1 = ind1 + 1
               if ( dabs(V(ind1)).ge.thresh ) then
                  do 205 mn = 1 , NMN
                     V(ind3+mn) = V(ind3+mn) - V(ind1)*V(ind2+mn)
 205              continue
               endif
               ind2 = ind2 + NMN
 220        continue
            ind1s = ind1s + NIJ
            ind3 = ind3 + NMN
 250     continue
      else
         do 300 kl = 1 , klbloc
            ind1 = ind1s
            ind2 = iv2
            do 260 ij = 1 , ijbloc
               ind1 = ind1 + 1
               if ( dabs(V(ind1)).ge.thresh ) then
                  do 255 mn = 1 , NMN
                     V(ind3+mn) = V(ind3+mn) + V(ind1)*V(ind2+mn)
 255              continue
               endif
               ind2 = ind2 + NMN
 260        continue
            ind1s = ind1s + NIJ
            ind3 = ind3 + NMN
 300     continue
      endif
C
      ijind = ijind + ijbloc
      if ( ijleft.gt.0 ) goto 200
      leng = klbloc*NMN
      call fileio(1,IBUC3,leng,V(iv31),0)
      if ( klleft.gt.0 ) goto 100
      return
C
      end
@* INDEX.
