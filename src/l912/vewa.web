@* vewa.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[PASSED:] IBUCA                                                     
\item[INPUT:] IBUCB                                                     
\item[INPUT:] DE                                                        
\item[INPUT:] EV                                                        
\item[INPUT:] NO                                                        
\item[INPUT:] NV                                                        
\item[INPUT:] IOPT                                                      
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]    V                                                      
\item[Uses]  MDV     V       ZERO                                   
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       FILEIO  TRACK                                          
\item[Called by]   TEWA                                                   
                                                                    
\end{description}
@a
      double precision function vewa(IBUCA,IBUCB,DE,EV,NO,NV,IOPT)
      implicit none
      double precision a0 , DE , eabij , eaij , eij , EV , F42 , Four , 
     &                 Half , One , Onept5 , Ten , Three , Two , V , 
     &                 Zero
      integer i , ia , iap , ib , IBUCA , IBUCB , ii , iip , ij , ind , 
     &        IOPT , kij , left , leng , lij , Mdv , mij , nij , NO , 
     &        no1
      integer no3 , nrij , nruns , NV , nv1 , nv3
      dimension EV(*)
      common /v     / V(20000) , Mdv
      common /const / Zero , Half , One , Onept5 , Two , Three , Four , 
     &                Ten , F42
C
C     ******************************************************************
C1vewa
C
C     iopt=1, forms
C     v(s) = a(s) * (de-ea-eb+ei+ej)
C     iopt=2, forms
C     v(s) = a(s) / (de-ea-eb+ei+ej)
C     the value of the function is:
C     vewa = sum  a(s) * v(s)
C            s>0
C
C     ibuca contains the a vector
C     ibucb contains v upon return
C     if ibucb=0, the output bucket is not written
C     ev contains the one-electron energies
C
C     april 77/seeger
C     ******************************************************************
C
C
C
      call track('VEWA  ')
C
      vewa = Zero
      no1 = NO - 1
      nv1 = NV - 1
      if ( no1.gt.0 .and. nv1.gt.0 ) then
C
         no3 = NO*no1/2
         nv3 = NV*nv1/2
         nrij = Mdv/nv3
         nruns = no3/nrij
         if ( mod(no3,nrij).ne.0 ) nruns = nruns + 1
         left = no3
         call fileio(2,-IBUCA,0,0,0)
         if ( IBUCB.gt.0 ) call fileio(1,-IBUCB,0,0,0)
         mij = 1
         lij = 0
C
         do 50 i = 1 , nruns
            nij = min0(left,nrij)
            left = left - nij
            leng = nij*nv3
            call fileio(2,IBUCA,leng,V,0)
C
            ind = 0
            kij = 0
            lij = lij + nij
            do 20 ii = 1 , no1
               iip = ii + 1
               do 10 ij = iip , NO
                  kij = kij + 1
                  if ( kij.le.lij ) then
                     if ( kij.ge.mij ) then
C     eij=de+ev(ii)+ev(ij)
                        eij = (DE) + EV(ii) + EV(ij)
                        if ( IOPT.eq.2 ) then
C
                           do 4 ia = 1 , nv1
                              iap = ia + 1
                              eaij = eij - EV(ia+NO)
                              do 2 ib = iap , NV
                                 ind = ind + 1
C     eabij=eaij-ev(ib+no)
                                 eabij = (eaij-EV(ib+NO))
                                 a0 = V(ind)/eabij
                                 vewa = vewa + a0*V(ind)
                                 V(ind) = a0
 2                            continue
 4                         continue
                        else
C
                           do 8 ia = 1 , nv1
                              iap = ia + 1
                              eaij = eij - EV(ia+NO)
                              do 6 ib = iap , NV
                                 ind = ind + 1
C     eabij=eaij-ev(ib+no)
                                 eabij = (eaij-EV(ib+NO))
                                 a0 = V(ind)*eabij
                                 vewa = vewa + a0*V(ind)
                                 V(ind) = a0
 6                            continue
 8                         continue
                        endif
                     endif
                  endif
 10            continue
 20         continue
C
C     output
            if ( IBUCB.gt.0 ) call fileio(1,IBUCB,leng,V,0)
C
            mij = mij + nij
 50      continue
      endif
C
      return
C
      end
@* INDEX.
