@* tstc3.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[INPUT:] MAXAP3                                                    
\item[INPUT:] A                                                         
\item[PASSED:] B                                                         
\item[INPUT:] NATOMS                                                    
\item[PASSED:] ATMCHG                                                    
\item[INPUT:] IAT                                                       
\item[INPUT:] JAT                                                       
\item[INPUT:] KAT                                                       
\item[OUTPUT:] CENTR                                                     
\item[OUTPUT:] ITST                                                      
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]     NOTHING                                           
\item[Uses]  TOLER                                                  
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       EQUIV   GABS    GATAN   PUT     ROTATE  TRIANG         
\item[Called by]   SPHERE                                                 
                                                                    
\end{description}
@a
      subroutine tstc3(MAXAP3,A,B,NATOMS,ATMCHG,IAT,JAT,KAT,CENTR,ITST)
      implicit none
      double precision A , alpha , ATMCHG , B , beta , CENTR , dij , 
     &                 dik , djk , eight , fact3 , gabs , gamma , 
     &                 gatan , half , one , phi3 , px , py , pz
      double precision t , theta3 , three , Tol2 , Toler , two
      integer IAT , ITST , JAT , KAT , MAXAP3 , NATOMS , numatm
      dimension A(MAXAP3,3) , CENTR(3)
      dimension t(3,3) , B(*) , ATMCHG(*)
      common /tol   / Toler , Tol2
      data half , one , two , three , eight/0.5D0 , 1.0D0 , 2.0D0 , 
     &     3.0D0 , 8.0D0/
C
C
C     are the three atoms iat, jat, and kat interchangeable via a
C     3-fold rotation
C        if no  itst=0, return
C        if yes itst=1, align c3 with z, return
C     centr is the point of intersection of the c3 axis with the
C     plane defined by the 3 atoms.
C
C
C
C
C
      numatm = NATOMS + 3
      ITST = 0
      phi3 = (eight/three)*gatan(one)
      theta3 = half*phi3
      fact3 = two/three
C
C     get the angles and sides of the triangle defined by the three
C     atoms.
C
      call triang(MAXAP3,A,IAT,JAT,KAT,alpha,beta,gamma,dij,dik,djk)
C
C     do the three points form an equilateral triangle  it is only
C     necessary to check to see if one angle is 60 degrees and that
C     two sides are equal.
C
      if ( gabs(alpha-theta3).gt.Toler .or. gabs(dij-dik).gt.Toler )
     &     return
      px = half*(A(JAT,1)+A(KAT,1))
      py = half*(A(JAT,2)+A(KAT,2))
      pz = half*(A(JAT,3)+A(KAT,3))
      CENTR(1) = A(IAT,1) + fact3*(px-A(IAT,1))
      CENTR(2) = A(IAT,2) + fact3*(py-A(IAT,2))
      CENTR(3) = A(IAT,3) + fact3*(pz-A(IAT,3))
      call put(MAXAP3,A,B,t,CENTR,numatm,3)
      call rotate(MAXAP3,A,B,NATOMS,t,3,phi3)
      call equiv(MAXAP3,A,B,ATMCHG,NATOMS,ITST)
      return
C
      end
@* INDEX.
