@* tstc5.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[INPUT:] MAXAP3                                                    
\item[INPUT:] A                                                         
\item[PASSED:] B                                                         
\item[INPUT:] NATOMS                                                    
\item[PASSED:] ATMCHG                                                    
\item[INPUT:] IAT                                                       
\item[INPUT:] JAT                                                       
\item[INPUT:] KAT                                                       
\item[OUTPUT:] CENTR                                                     
\item[OUTPUT:] ITST                                                      
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]     NOTHING                                           
\item[Uses]  TOLER                                                  
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       EQUIV   GABS    GATAN   GSIN    PUT     ROTATE  TRIANG 
\item[Called by]   SPHERE                                                 
                                                                    
\end{description}
@a
      subroutine tstc5(MAXAP3,A,B,NATOMS,ATMCHG,IAT,JAT,KAT,CENTR,ITST)
      implicit none
      double precision A , alpha , ATMCHG , B , beta , CENTR , dij , 
     &                 dik , djk , fact5 , gabs , gamma , gatan , gsin , 
     &                 half , one , onept6 , phi5 , piovr4 , pt8
      double precision px , py , pz , t , theta5 , Tol2 , Toler , two , 
     &                 twopt4
      integer IAT , ITST , JAT , KAT , MAXAP3 , NATOMS , numatm
      dimension A(MAXAP3,3) , CENTR(3)
      dimension t(3,3) , B(*) , ATMCHG(*)
      common /tol   / Toler , Tol2
      data half , pt8 , one , onept6/0.5D0 , 0.8D0 , 1.0D0 , 1.6D0/
      data two , twopt4/2.0D0 , 2.4D0/
C
C
C     are the three atoms iat, jat, and kat interchangeable via a
C     5-fold rotation
C        if no  itst=0, return
C        if yes itst=1, align c5 with z, return
C     centr is the point of intersection of the c5 axis with the
C     plane defined by the 3 atoms.
C
C
C
C
C
      numatm = NATOMS + 3
      ITST = 0
      piovr4 = gatan(one)
      phi5 = onept6*piovr4
      theta5 = twopt4*piovr4
      fact5 = one/(two*gsin(pt8*piovr4)**2)
C
C     get the angles and sides of the triangle defined by the three
C     atoms.
C
      call triang(MAXAP3,A,IAT,JAT,KAT,alpha,beta,gamma,dij,dik,djk)
C
C     are any of these angles equal to 108 degrees (i.e. the internal
C     angle of a regular pentagon) and thus possibly equivalent by a
C     5-fold axis of symmetry
C     are two of the sides of the triangle of equal length
C
      if ( gabs(alpha-theta5).le.Toler .and. gabs(dij-dik).le.Toler )
     &     then
         px = half*(A(JAT,1)+A(KAT,1))
         py = half*(A(JAT,2)+A(KAT,2))
         pz = half*(A(JAT,3)+A(KAT,3))
         CENTR(1) = A(IAT,1) + fact5*(px-A(IAT,1))
         CENTR(2) = A(IAT,2) + fact5*(py-A(IAT,2))
         CENTR(3) = A(IAT,3) + fact5*(pz-A(IAT,3))
C
      elseif ( gabs(beta-theta5).gt.Toler .or. gabs(dij-djk).gt.Toler )
     &         then
C
         if ( gabs(gamma-theta5).gt.Toler .or. gabs(dik-djk).gt.Toler )
     &        return
         px = half*(A(IAT,1)+A(JAT,1))
         py = half*(A(IAT,2)+A(JAT,2))
         pz = half*(A(IAT,3)+A(JAT,3))
         CENTR(1) = A(KAT,1) + fact5*(px-A(KAT,1))
         CENTR(2) = A(KAT,2) + fact5*(py-A(KAT,2))
         CENTR(3) = A(KAT,3) + fact5*(pz-A(KAT,3))
      else
         px = half*(A(IAT,1)+A(KAT,1))
         py = half*(A(IAT,2)+A(KAT,2))
         pz = half*(A(IAT,3)+A(KAT,3))
         CENTR(1) = A(JAT,1) + fact5*(px-A(JAT,1))
         CENTR(2) = A(JAT,2) + fact5*(py-A(JAT,2))
         CENTR(3) = A(JAT,3) + fact5*(pz-A(JAT,3))
      endif
C
      call put(MAXAP3,A,B,t,CENTR,numatm,3)
      call rotate(MAXAP3,A,B,NATOMS,t,3,phi5)
      call equiv(MAXAP3,A,B,ATMCHG,NATOMS,ITST)
      return
C
      end
@* INDEX.
