@* csymm.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[PASSED:] A                                                         
\item[INPUT:] MDIM                                                      
\item[INPUT:] NDIM                                                      
\item[INPUT:] CMP                                                       
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]     NOTHING                                           
\item[Uses]  IOUT                                                   
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       CARPOL  DSYMM                                          
\item[Called by]   FIRST   SCFDM                                          
                                                                    
\end{description}
@a
      subroutine csymm(A,MDIM,NDIM,CMP)
      implicit none
      double precision A , phi , r
      integer i , iad , ii , In , Iout , Ipunch , j , jlim , ll , lu , 
     &        MDIM , mtt , NDIM , ntt
      logical CMP
      dimension A(*) , r(5) , phi(5)
      common /io    / In , Iout , Ipunch
C     ******************************************************************
C     print output routine for a complex (or real) matrix in
C     symmetric storage mode (lower triangle)
C     in polar coordinates
C
C     dec 76/rs9b
C     ******************************************************************
C
C
99001 format (6x,5(i16,9x))
99002 format (i4,2x,5(f14.7,f11.7))
C     ******************************************************************
      if ( CMP ) then
C
C     complex
         mtt = MDIM*(MDIM+1)/2
         ntt = NDIM*(NDIM+1)/2
         ll = 1
 50      lu = min0(NDIM,ll+4)
         write (Iout,99001) (i,i=ll,lu)
         do 100 i = ll , NDIM
            iad = i*(i-1)/2
            ii = min0(i,lu)
C     convert one sub-row to polar form.
            jlim = ii - ll + 1
            do 60 j = 1 , jlim
               call carpol(A(iad+j),A(iad+j+mtt),r(j),phi(j))
 60         continue
C     print the sub-row.
            write (Iout,99002) i , (r(j),phi(j),j=1,jlim)
 100     continue
         ll = lu + 1
         if ( ll.le.NDIM ) goto 50
         return
      endif
C
C     real
      call dsymm(A,NDIM)
      return
C
      end
@* INDEX.
