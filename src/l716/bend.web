@* bend.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[INPUT:] NOINT                                                     
\item[INPUT:] I                                                         
\item[INPUT:] J                                                         
\item[INPUT:] K                                                         
\item[OUTPUT:] B                                                         
\item[OUTPUT:] IB                                                        
\item[INPUT:] C                                                         
\item[INPUT:] NPARM                                                     
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]     NOTHING                                           
\item[Uses]   NOTHING                                           
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       GSQRT                                                  
\item[Called by]   FORMBG                                                 
                                                                    
\end{description}
@a
      subroutine bend(NOINT,I,J,K,B,IB,C,NPARM)
C*****subroutine bend(noint,i,j,k,b,ib,c)
      implicit none
      double precision B , C , dji , djisq , djk , djksq , dotj , eji , 
     &                 ejk , gsqrt , one , rji , rjk , sinj , zero
      integer I , iaind , IB , J , jaind , K , kaind , m , NOINT , NPARM
      dimension B(3,4,NPARM) , IB(4,NPARM) , C(*)
C*****dimension b(3,4,2),ib(4,2),c(*)
      dimension rji(3) , rjk(3) , eji(3) , ejk(3)
      data zero/0.D0/ , one/1.D0/
C
C
C
C        adapted from the normal coordinate analysis program of
C        schachtschneider, shell development .
C
C
C
C
      iaind = 3*(I-1)
      jaind = 3*(J-1)
      kaind = 3*(K-1)
      IB(1,NOINT) = I
      IB(2,NOINT) = J
      IB(3,NOINT) = K
      djisq = zero
      djksq = zero
      do 100 m = 1 , 3
         rji(m) = C(m+iaind) - C(m+jaind)
         rjk(m) = C(m+kaind) - C(m+jaind)
         djisq = djisq + rji(m)**2
         djksq = djksq + rjk(m)**2
 100  continue
      dji = gsqrt(djisq)
      djk = gsqrt(djksq)
      dotj = zero
      do 200 m = 1 , 3
         eji(m) = rji(m)/dji
         ejk(m) = rjk(m)/djk
         dotj = dotj + eji(m)*ejk(m)
 200  continue
      sinj = gsqrt(one-dotj**2)
      do 300 m = 1 , 3
         B(m,3,NOINT) = ((dotj*ejk(m)-eji(m)))/(djk*sinj)
         B(m,1,NOINT) = ((dotj*eji(m)-ejk(m)))/(dji*sinj)
         B(m,2,NOINT) = -B(m,1,NOINT) - B(m,3,NOINT)
 300  continue
C
      return
C
      end
@* INDEX.
