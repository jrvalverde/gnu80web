@* ehobkd.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[INPUT:] A                                                         
\item[INPUT:] N                                                         
\item[INPUT:] M1                                                        
\item[INPUT:] M2                                                        
\item[OUTPUT:] Z                                                         
\item[INPUT:] IZ                                                        
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]     NOTHING                                           
\item[Uses]   NOTHING                                           
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]        NOTHING                                           
\item[Called by]   DIAGD                                                  
                                                                    
\end{description}
@a
      subroutine ehobkd(A,N,M1,M2,Z,IZ)
      implicit none
      double precision A , h , s , Z , zero
      integer i , ia , IZ , j , k , l , M1 , M2 , N
      dimension A(*) , Z(IZ,*)
      data zero/0.D0/
C
C   function            - perform a back transformation to form the
C                           eigenvectors of the original symmetric
C                           matrix from the eigenvectors of the
C                           tridiagonal matrix.
C   parameters   a      - the array contains the details of the house
C                           holder reduction of the original matrix a as
C                           generated by subroutine 'ehousd'.
C                n      - order of the real symmetric matrix.
C                m1     - m1 and m2 are two input scalars such that
C                           eigenvectors m1 to m2 of the tridiagonal
C                           matrix a have been found and normalized
C                           according to the euclidean norm.
C                m2     - see above - m1
C                z      - a two dimensional array of size n x (m2-m1+1)
C                           which contains eigenvectors m1 to m2 of
C                           tridiagonal matrix t, normalized according
C                           to euclidean norm. input z can be produced
C                           by subroutine eqrt2d, the resultant
C                           matrix overwrites the input z.
C                iz     - row dimension of z in calling program.
C      implemented by rolf seeger
C      numer. math., 11, 181 (1968)
C
C
      if ( N.ne.1 ) then
         do 50 i = 2 , N
            l = i - 1
            ia = i*l/2
            h = A(ia+i)
            if ( h.ne.0 ) then
C                                  derives eigenvectors m1 to m2 of
C                                  the original matrix from eigenvectors
C                                  m1 to m2 of the symmetric
C                                  tridiagonal matrix
               do 20 j = M1 , M2
                  s = zero
                  do 5 k = 1 , l
                     s = s + A(ia+k)*Z(k,j)
 5                continue
                  s = s/h
                  do 10 k = 1 , l
                     Z(k,j) = Z(k,j) - s*A(ia+k)
 10               continue
 20            continue
            endif
 50      continue
      endif
      return
C
      end
@* INDEX.
