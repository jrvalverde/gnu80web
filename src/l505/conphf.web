@* conphf.
\section*{\sf Dummy Arguments}  \begin{description}
                                                                    
\item[INPUT:] N                                                         
\item[OUTPUT:] KEY                                                       
\item[OUTPUT:] ACURCY                                                    
\item[INPUT:] INSIDE                                                    
\item[OUTPUT:] IFLAG                                                     
                                                                    
\end{description} \section*{\sf Common Variables}  \begin{description}
                                                                    
\item[Modifies]    A1      A2      A3      ICOUNT  TRMSDP                 
\item[Transmits as argument]  IRWC1   IRWC2   NTT2                                   
\item[Uses]  A1      A2      A3      ICOUNT  IEXT    IOUT    IRWC1  
             IRWC2   IRWC3   IRWC4   MAXNTT  NTT     NTT2    PSAVE  
             TRMSDP                                                 
                                                                    
\end{description}  \section*{\sf External Subprograms}  \begin{description}
                                                                    
\item[Calls]       GABS    GSQRT   TRAPHF  TREAD   TWRITE                 
\item[Called by]   CYCPHF                                                 
                                                                    
\end{description}
@a
      subroutine conphf(N,KEY,ACURCY,INSIDE,IFLAG)
      implicit none
      double precision A1 , A2 , A3 , ACURCY , cosphi , cospsi , dp1 , 
     &                 dp2 , dp3 , Filabc , four , gabs , gsqrt , one , 
     &                 onept9 , pt99 , pt995 , rmsdpi , sp11 , sp12
      double precision sp13 , sp22 , sp23 , sp33 , traphf , Trmsdp , 
     &                 two , x , xxx , xy , y , yyy , zero
      integer i , i1 , i2 , Ia , Icnvg , Icount , Icyc , Idumifill , 
     &        Idump , Iext , Ifcnvg , IFLAG , Iguess , In , INSIDE , 
     &        Iout , Ipch , Iprint , Ipunch , Irstrt
      integer Irwc1 , Irwc2 , Irwc3 , Irwc4 , Irwca , Irwcb , Irwev , 
     &        Irwfa , Irwfb , Irwgen , Irwh , Irwpa , Irwpb , Irwpt , 
     &        Irws , Irwt , KEY , loc1 , loc2 , Maxnbf
      integer Maxntt , N , nam3 , nam4 , Ntt , Ntt2 , nttm2
      integer Psave
      dimension nam3(2) , nam4(2)
C      common /abc505/a3(4970),a2(4970),a1(4970),idum
      common /memry / A3(4970) , A2(4970) , A1(4970) , Idumifill , 
     &                Filabc(35089)
      common /psave / Psave
      common /ops505/ Ipch , Iprint , Idump , Iguess , Icnvg , Icyc , 
     &                Irstrt , Iext , Ifcnvg
      common /irw505/ Irwgen , Irws , Irwt , Irwh , Irwev , Irwca , 
     &                Irwcb , Irwpa , Irwpb , Irwpt , Irwfa , Irwfb , 
     &                Irwc1 , Irwc2 , Irwc3 , Irwc4
      common /max505/ Maxnbf , Maxntt
      common /rhfcvg/ Ntt2 , Icount , Trmsdp
      common /io    / In , Iout , Ipunch
      common /jnkphf/ Ntt , Ia(71)
      data pt99/0.99D0/ , pt995/0.995D0/ , one/1.0D0/ , onept9/1.90D0/
      data two/2.0D0/ , four/4.0D0/ , zero/0.0D0/
      data nam3/4H3-PO , 4HINT./ , nam4/4H4-PO , 4HINT./
C
C
C
C
C
C
99001 format (' ',30x,d11.4)
99002 format (' ',48x,2A4)
C
C
      KEY = 1
C
C     signal no extrapolation.
      IFLAG = 0
      nttm2 = 2*Maxntt
C
C     ******************************************************************
C     *  compress p(alpha) and p(beta) into one array.                 *
C     ******************************************************************
      do 100 i = 1 , Ntt
         A3(Ntt+i) = A3(Maxntt+i)
 100  continue
C     ******************************************************************
C     *  read previous alpha-beta matrix into a1.                      *
C     ******************************************************************
      call tread(Irwc1,A1,nttm2,1,Ntt2,1,0)
C     ******************************************************************
C     *  form the difference in a2 and twrite a3 on 30.                *
C     ******************************************************************
      do 200 i = 1 , Ntt2
         A2(i) = A3(i) - A1(i)
 200  continue
      call twrite(Irwc1,A3,nttm2,1,Ntt2,1,0)
C     ******************************************************************
C     *  find rmsdp for the present subcycle.                          *
C     ******************************************************************
      sp11 = traphf(A2,A2,N)
      rmsdpi = gsqrt(sp11/two)/N
C     ******************************************************************
C     *  add to the previous total rmsdpi.                             *
C     ******************************************************************
      Trmsdp = Trmsdp + rmsdpi
      if ( INSIDE.le.2 ) then
C
         return
      else
C     ******************************************************************
C     *  test for convergence.                                         *
C     ******************************************************************
         Icount = Icount + 1
         if ( Icount.ne.1 ) then
            if ( Trmsdp.lt.gabs(ACURCY) ) then
               KEY = 0
               ACURCY = Trmsdp
            else
C
               loc1 = Irwc4
               if ( Icount.ne.(Icount/2)*2 ) loc1 = Irwc3
               loc2 = (Irwc3+Irwc4) - loc1
C     ******************************************************************
C     *  p(n) is now in a3.  read p(n-1) in to a1.                     *
C     ******************************************************************
               call tread(Irwc2,A1,nttm2,1,Ntt2,1,0)
C     ******************************************************************
C     *  form p(n) - p(n-1) in a2.                                     *
C     ******************************************************************
               do 210 i = 1 , Ntt2
                  A2(i) = A3(i) - A1(i)
 210           continue
C     ******************************************************************
C     *  find length dp1.                                              *
C     ******************************************************************
               sp11 = traphf(A2,A2,N)
               dp1 = gsqrt(sp11/two)
               if ( Icount.ne.2 ) then
C
                  if ( Icount.ge.4 ) then
                     call tread(loc1,A1,nttm2,1,Ntt2,1,0)
                     sp23 = sp12
                     sp33 = sp22
                     sp13 = traphf(A1,A2,N)
C     ******************************************************************
C     *  find length dp3.                                              *
C     ******************************************************************
                     dp3 = gsqrt(sp33/two)
                  endif
C     ******************************************************************
C     *  read p(n-1) - p(n-2)  into a1.                                *
C     ******************************************************************
                  call tread(loc2,A1,nttm2,1,Ntt2,1,0)
                  sp12 = traphf(A1,A2,N)
                  sp22 = traphf(A1,A1,N)
C     ******************************************************************
C     *  find length dp2.                                              *
C     ******************************************************************
                  dp2 = gsqrt(sp22/two)
C     ******************************************************************
C     *  find cosine of angle between successive displacements.        *
C     ******************************************************************
                  cosphi = sp12/(two*dp1*dp2)
                  if ( Icount.ne.3 ) then
C     ******************************************************************
C     *  find cosine of angle between dp3 and plane of dp1 and dp2.    *
C     ******************************************************************
                     x = (sp13*sp22-sp12*sp23)/(sp11*sp22-sp12*sp12)
                     y = (sp23*sp11-sp12*sp13)/(sp11*sp22-sp12*sp12)
                     cospsi = gsqrt((x*x*sp11+y*y*sp22+two*x*y*sp12)
     &                        /two)/dp3
                     if ( Psave.eq.0 ) write (Iout,99001) Trmsdp
                     if ( Iext.ne.2 ) then
C     ****************************************************************
C     *  do not extrapolate unless four consecutive points are         *
C     *  nearly coplanar.                                              *
C     ******************************************************************
                        if ( cospsi.gt.pt99 ) then
C     ******************************************************************
C     *  express vector dp1 as x*dp3(projected)+y*dp2.                 *
C     ******************************************************************
                           y = -y/x
                           x = one/x
C     ******************************************************************
C     *  test if 2*2 matrix has real eigenvalues between -0.95 and     *
C     *  +0.95.                                                        *
C     ******************************************************************
                           xy = y*y + four*x
                           if ( xy.ge.0 ) then
                              xy = gabs(y) + gsqrt(xy)
                              if ( xy.le.onept9 ) then
C
                                 if ( Iext.lt.2 ) then
                                    xxx = x/(one-x-y)
                                    yyy = (x+y)/(one-x-y)
                                    do 212 i = 1 , Ntt2
                                       A3(i) = A3(i) + xxx*A1(i)
     &                                    + yyy*A2(i)
 212                                continue
                                    if ( Psave.eq.0 ) write (Iout,99002)
     &                                 nam4
                                    IFLAG = 1
                                    Icount = 0
                                 endif
                                 goto 220
                              endif
                           endif
C     ******************************************************************
C     *  if four point extrapolation is not possible, try the three    *
C     *  point extrapolation.                                          *
C     ******************************************************************
                           if ( Iext.lt.1 ) then
                              if ( gabs(cosphi).gt.pt995 ) then
                                 x = dp1/(dp2*cosphi-dp1)
                                 do 214 i = 1 , Ntt2
                                    A3(i) = A3(i) + x*A2(i)
 214                             continue
                                 if ( Psave.eq.0 ) write (Iout,99002)
     &                                nam3
                                 IFLAG = 1
                                 Icount = 0
                              endif
                           endif
                        endif
                     endif
                  else
                     if ( Psave.eq.0 ) write (Iout,99001) Trmsdp
                  endif
               else
                  if ( Psave.eq.0 ) write (Iout,99001) Trmsdp
               endif
 220           do 230 i = 1 , Ntt2
                  A1(i) = A2(i)
 230           continue
               call twrite(loc1,A1,nttm2,1,Ntt2,1,0)
            endif
         endif
      endif
      do 300 i = 1 , Ntt2
         A1(i) = A3(i)
 300  continue
      call twrite(Irwc2,A1,nttm2,1,Ntt2,1,0)
C     ******************************************************************
C     *  separate alpha and beta density matrices.                     *
C     ******************************************************************
      do 400 i = 1 , Ntt
         i1 = (Maxntt+1) + Ntt - i
         i2 = Ntt2 + 1 - i
         A3(i1) = A3(i2)
 400  continue
      Trmsdp = zero
      return
C
C
C
      end
@* INDEX.
