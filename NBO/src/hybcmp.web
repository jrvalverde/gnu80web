@*hybcmp.
\section*{\sf Dummy Arguments}  \begin{description}

\item[OUTPUT:] XYZ
\item[OUTPUT:] PCENT
\item[INPUT:] IHYB
\item[INPUT:] JCTR
\item[OUTPUT:] HYB

\end{description} \section*{\sf Common Variables}  \begin{description}

\item[Modifies]     NOTHING 
\item[Uses]  LFNPR   NAOA    NAOC    NBAS

\end{description}  \section*{\sf External Subprograms}  \begin{description}

\item[Calls]        NOTHING 
\item[Called by]   HYBDIR

\end{description}
@a
C*****************************************************************************
      subroutine hybcmp(XYZ,PCENT,IHYB,JCTR,HYB)
C*****************************************************************************
      implicit none
      double precision cutoff , hnorm , HYB , PCENT , thresh , tmax ,
     &                 XYZ , zero
      integer IHYB , inao , Iprin , Ispin , ix , JCTR , jmax , l ,
     &        Lfnao , Lfnarc , Lfndaf , Lfndef , Lfndm , Lfnin , Lfnmo ,
     &        Lfnnab , Lfnnao , Lfnnbo , Lfnnho , Lfnnlm
      integer Lfnpna , Lfnpnb , Lfnpnh , Lfnpnl , Lfnppa , Lfnpr ,
     &        Ltyp , m , MAXATM , MAXBAS , Munit , Mxao , Mxaolm ,
     &        Mxbo , Naoa , Naoc , Natoms , Nbas , Ndim
      dimension XYZ(3) , HYB(1)
C
      parameter (MAXATM=99,MAXBAS=500)
      common /nbnao / Naoc(MAXBAS) , Naoa(MAXBAS) , Ltyp(MAXBAS) ,
     &                Iprin(MAXBAS)
      common /nbinfo/ Ispin , Natoms , Ndim , Nbas , Mxbo , Mxao ,
     &                Mxaolm , Munit
      common /nbio  / Lfnin , Lfnpr , Lfnao , Lfnpna , Lfnnao , Lfnpnh ,
     &                Lfnnho , Lfnpnb , Lfnnbo , Lfnpnl , Lfnnlm ,
     &                Lfnmo , Lfndm , Lfnnab , Lfnppa , Lfnarc ,
     &                Lfndaf , Lfndef
C
      data zero , thresh , cutoff/0.0D0 , 1.0D-4 , 1.0D-8/
C
C  add the px,py,pz components of this hybrid vectorially and determine
C  its percentage p-character:
C
      XYZ(1) = zero
      XYZ(2) = zero
      XYZ(3) = zero
      PCENT = zero
      hnorm = zero
C
C  make sure this hybrid is situated on the correct atom, jctr:
C
      jmax = 1
      tmax = dabs(HYB(1))
      do 100 inao = 2 , Nbas
         if ( dabs(HYB(inao)).gt.tmax ) then
            jmax = inao
            tmax = dabs(HYB(inao))
         endif
 100  continue
      if ( Naoc(jmax).ne.JCTR ) then
         write (Lfnpr,99001) IHYB , JCTR , Naoc(jmax)
         stop
      endif
C
C  find the sign of the largest s-component of this hybrid:
C
      jmax = 0
      tmax = zero
      do 200 inao = 1 , Nbas
         l = Naoa(inao)/100
         if ( l.eq.0 .and. dabs(HYB(inao)).gt.tmax ) then
            jmax = inao
            tmax = dabs(HYB(inao))
         endif
 200  continue
C
C  if the sign of the largest s-component is negative, change the
C  phase of this hybrid:
C
      if ( jmax.ne.0 .and. HYB(jmax).lt.-thresh ) then
         do 250 inao = 1 , Nbas
            HYB(inao) = -HYB(inao)
 250     continue
      endif
C
C  sum the px,py,pz components of this hybrid, determine the percent
C  p-character:
C
      do 300 inao = 1 , Nbas
         if ( Naoc(inao).eq.JCTR ) then
            l = Naoa(inao)/100
            if ( l.eq.1 ) then
               PCENT = PCENT + HYB(inao)*HYB(inao)
               m = mod(Naoa(inao),50)
               XYZ(m) = XYZ(m) + HYB(inao)
            endif
            hnorm = hnorm + HYB(inao)*HYB(inao)
         endif
 300  continue
      if ( hnorm.lt.thresh ) then
         write (Lfnpr,99002) JCTR , IHYB
         stop
      endif
      PCENT = PCENT/hnorm*100.0
C
C  normalize the px,py,pz vector:
C
      hnorm = zero
      do 400 ix = 1 , 3
         if ( dabs(XYZ(ix)).lt.cutoff ) XYZ(ix) = zero
         hnorm = hnorm + XYZ(ix)*XYZ(ix)
 400  continue
      hnorm = dsqrt(hnorm)
      if ( dabs(hnorm).lt.cutoff ) then
         PCENT = zero
      else
         do 450 ix = 1 , 3
            XYZ(ix) = XYZ(ix)/hnorm
 450     continue
      endif
      return
C
99001 format (/1x,'Expected to find hybrid ',i3,' on nuclear center ',
     &        i2,' rather than center ',i2,'.')
99002 format (/1x,'The atomic orbitals on nuclear center ',i2,
     &        ' do not ','contribute to hybrid ',i3,'.')
      end
@* INDEX.
