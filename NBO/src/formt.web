@*formt.
\section*{\sf Dummy Arguments}  \begin{description}

\item[OUTPUT:] T
\item[INPUT:] Q
\item[INPUT:] POL

\end{description} \section*{\sf Common Variables}  \begin{description}

\item[Modifies]    IBX     IBXM    LABEL
\item[Uses]  IATHY   IBX     IBXM    LABEL   LFNPR   LL      MXAO
             NAOA    NBAS    NDIM    UL

\end{description}  \section*{\sf External Subprograms}  \begin{description}

\item[Calls]        NOTHING 
\item[Called by]   CHOOSE  NATHYB

\end{description}
@a
C*****************************************************************************
      subroutine formt(T,Q,POL)
C*****************************************************************************
      implicit none
      integer i , ia , iab , Iatcr , Iathy , Iatno , ib , ibd , ibo ,
     &        Ibx , Ibxm , icol , icr , il , ilp , in , Ino , Iprin ,
     &        irow , Ispin
      integer Iznuc , j , jb , jj , jl , jmax , ju , k , kbd , Label ,
     &        lbd , lcr , Lfnao , Lfnarc , Lfndaf , Lfndef , Lfndm ,
     &        Lfnin , Lfnmo , Lfnnab
      integer Lfnnao , Lfnnbo , Lfnnho , Lfnnlm , Lfnpna , Lfnpnb ,
     &        Lfnpnh , Lfnpnl , Lfnppa , Lfnpr , Ll , llp , lry ,
     &        lstar , Lstocc , Ltyp , MAXATM , MAXBAS , Munit , Mxao
      integer Mxaolm , Mxbo , Naoa , Naoc , Naoctr , Naol , Natoms ,
     &        Nbas , nbds , NBO , ncr , nctr , Ndim , nlp , Norbs ,
     &        nscan
      double precision POL , Q , T , tmax , zero
      integer Ul
C
C  construction of final transformation  matrix t from orthonormal
C  hybrids; rows of t labelled by naos, columns by nbos.
C
      parameter (MAXATM=99,MAXBAS=500)
      common /nbinfo/ Ispin , Natoms , Ndim , Nbas , Mxbo , Mxao ,
     &                Mxaolm , Munit
      common /nbbas / Label(MAXBAS,6) , Naoc(MAXBAS) , Naol(MAXBAS) ,
     &                Lstocc(MAXBAS) , Ibxm(MAXBAS) , Ibx(MAXBAS) ,
     &                Iathy(MAXBAS,3)
      common /nbatom/ Iatno(MAXATM) , Ino(MAXATM) , Norbs(MAXATM) ,
     &                Ll(MAXATM) , Ul(MAXATM) , Iznuc(MAXATM) ,
     &                Iatcr(MAXATM)
      common /nbnao / Naoctr(MAXBAS) , Naoa(MAXBAS) , Ltyp(MAXBAS) ,
     &                Iprin(MAXBAS)
      common /nbio  / Lfnin , Lfnpr , Lfnao , Lfnpna , Lfnnao , Lfnpnh ,
     &                Lfnnho , Lfnpnb , Lfnnbo , Lfnpnl , Lfnnlm ,
     &                Lfnmo , Lfndm , Lfnnab , Lfnppa , Lfnarc ,
     &                Lfndaf , Lfndef
      dimension T(Ndim,Ndim) , Q(Mxao,Ndim) , POL(Ndim,3)
C
      data lcr , llp , lbd , lstar , lry/'CR' , 'LP' , 'BD' , '*' ,
     &     'RY'/
      data zero/0.0D0/
C
C  reorder occupied nbos to put lone and core pairs last:
C
      ncr = 0
      nlp = 0
      nbds = 0
      do 100 nscan = 1 , Nbas
         if ( Label(nscan,2).ne.lstar ) then
            nbds = nbds + 1
            if ( Label(nscan,1).eq.llp ) nlp = nlp + 1
            if ( Label(nscan,1).eq.lcr ) ncr = ncr + 1
         endif
 100  continue
      icr = 0
      ilp = 0
      ibo = 0
      iab = 0
      do 200 ibd = 1 , Nbas
         if ( Label(ibd,2).eq.lstar ) then
C
C  antibonds:
C
            iab = iab + 1
            Ibx(ibd) = nbds + iab
         elseif ( Label(ibd,1).eq.lcr ) then
C
C  core pairs:
C
            icr = icr + 1
            Ibx(ibd) = icr + nbds - ncr - nlp
         elseif ( Label(ibd,1).eq.llp ) then
C
C  lone pairs and core pairs:
C
            ilp = ilp + 1
            Ibx(ibd) = ilp + nbds - nlp
         else
C
C  pair bonds:
C
            ibo = ibo + 1
            Ibx(ibd) = ibo
         endif
 200  continue
C
C  zero transformation array:
C
      do 300 i = 1 , Nbas
         do 250 j = 1 , Nbas
            T(i,j) = zero
 250     continue
 300  continue
C
C  deposit final bond orbitals in matrix t:
C
      NBO = 0
      do 500 ibd = 1 , Nbas
         kbd = ibd
         if ( Label(ibd,2).eq.lstar ) then
            if ( Label(ibd,1).ne.lry ) then
               if ( Label(ibd,1).ne.llp ) then
C
C  antibond orbitals: search occupied orb. list to get proper hybrids.
C  search occupied bond orbs. for match with antibond atoms:
C
                  do 305 k = 1 , nbo
                     do 302 i = 4 , 6
                        if ( Label(k,i).ne.Label(ibd,i) ) goto 305
                        if ( (Label(k,3).le.0) .and. (Label(k,1).eq.lbd)
     &                       ) goto 305
 302                 continue
C
C  negative irnk = label(k,3) means bond orbital was already used:
C
C  found match; set label(k,3)<0:
C
                     kbd = k
                     Label(kbd,3) = -Label(kbd,3)
                     goto 350
 305              continue
C
C  couldn't find match...exit:
C
                  write (Lfnpr,99001) ibd , (Label(ibd,jj),jj=1,6)
                  stop
               endif
            endif
         endif
C
C  deposit bond orbitals in t matrix:
C
 350     do 400 i = 1 , 3
            ia = Label(ibd,i+3)
            if ( ia.ne.0 ) then
               jl = Ll(ia)
               ju = Ul(ia)
               irow = 0
               icol = jl + Iathy(kbd,i) - 1
               do 360 j = jl , ju
                  irow = irow + 1
                  jb = Ibx(ibd)
                  T(j,jb) = POL(ibd,i)*Q(irow,icol)
 360           continue
            endif
 400     continue
         if ( ibd.eq.kbd ) NBO = ibd
 500  continue
C
C  restore label(i,3) > 0:
C
      do 600 i = 1 , Nbas
         if ( Label(i,3).lt.0 ) Label(i,3) = -Label(i,3)
 600  continue
C
C  set array ibxm: ibxm(ib) is the current location of b.o.  \# ib:
C
      do 700 ib = 1 , Nbas
         i = Ibx(ib)
         Ibxm(i) = ib
 700  continue
C
C  set phase of 1-center orbitals such that the largest s-type NAO contribution
C  is positive:
C
      do 800 ib = 1 , Nbas
         nctr = 1
         do 750 il = 5 , 6
            if ( Label(Ibxm(ib),il).ne.0 ) nctr = nctr + 1
 750     continue
         if ( nctr.eq.1 ) then
            jmax = 0
            tmax = -1.0D0
            do 760 in = 1 , Nbas
               if ( Naoa(in).lt.100 ) then
                  if ( dabs(T(in,ib)).gt.tmax ) then
                     jmax = in
                     tmax = dabs(T(in,ib))
                  endif
               endif
 760        continue
            if ( jmax.ne.0 ) then
               if ( T(jmax,ib).lt.-1.0D-4 ) then
                  do 765 in = 1 , Nbas
                     T(in,ib) = -T(in,ib)
 765              continue
               endif
            endif
         endif
 800  continue
      return
C
99001 format (/,1x,'Can''t find bond/antibond match for NBO ',i3,2x,a2,
     &        a1,'(',i2,')',3I4)
      end
@* INDEX.
